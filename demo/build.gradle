plugins {
    id 'com.android.application'
}
//uploadArchives.enabled = false

ext.private = new Properties()
if (rootProject.file('private.properties').exists()) {
    ext.private.load(new FileInputStream(rootProject.file('private.properties')))
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        applicationId "cards.pay.sample.demo"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode Integer.parseInt(DEMO_APP_VERSION_CODE)
        versionName DEMO_APP_VERSION_NAME
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        resourceConfigurations += ['en']
    }
    signingConfigs {
        release
        debug
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            applicationIdSuffix ".dbg"
            minifyEnabled false
            jniDebuggable true
        }
        profiling {
            initWith debug
        }
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/LICENSE.txt', 'META-INF/NOTICE.txt']
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    namespace 'cards.pay.sample.demo'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(path: ":sdk")
    //debugCompile project(path: ":sdk", configuration: 'release')
    //releaseCompile project(path: ":sdk", configuration: 'release')
    androidTestImplementation "androidx.test.espresso:espresso-core:3.5.1"
    implementation "androidx.appcompat:appcompat:1.6.1"
    implementation "androidx.gridlayout:gridlayout:1.0.0"
    implementation "com.google.android.material:material:1.9.0"
    testImplementation "junit:junit:4.13.2"
}

if (ext.private.getProperty("release.StoreFile") != null) {
    android.signingConfigs['release'].storeFile = file(ext.private['release.StoreFile'])
    android.signingConfigs['release'].storePassword = ext.private['release.StorePassword']
    android.signingConfigs['release'].keyAlias = ext.private['release.KeyAlias']
    android.signingConfigs['release'].keyPassword = ext.private['release.KeyPassword']
} else {
    android.buildTypes['release'].signingConfig = null
}
